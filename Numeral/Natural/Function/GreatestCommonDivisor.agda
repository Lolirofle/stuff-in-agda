module Numeral.Natural.Function.GreatestCommonDivisor where

import Lvl
open import Functional
open import Numeral.CoordinateVector as Vector using (Vector)
open import Numeral.Finite using (ğ•Ÿ ; ğŸ ; ğ’)
open import Numeral.Natural
open import Numeral.Natural.Oper.Modulo
open import Numeral.Natural.Relation.Divisibility
open import Numeral.Natural.Function.GreatestCommonDivisor.Algorithm
open import Type

-- TODO: Prove that gcd is the infimum in a lattice of â„• with divisibility as its ordering

-- An algorithm for computing the greatest common divisor for two numbers.
-- Also called: Euclid's algorithm.
-- Termination: See `Gcd-existence` for a functionally equal variant of this function that passes the termination checker. It is equal in the sense that the same algorithm is used to construct the existence and to compute the value of this function. This is even more evident when looking at `Gcd-gcd`.
-- Alternative implementation:
--   gcd(a)(ğŸ) = a
--   gcd(a)(ğ’(b)) with [â‰¥]-or-[<] {a}{ğ’(b)}
--   ... | [âˆ¨]-introâ‚— _ = gcd(ğ’(b))(a mod ğ’(b))
--   ... | [âˆ¨]-introáµ£ _ = gcd(ğ’(b))(a)
gcd : â„• â†’ â„• â†’ â„•
gcd = gcdAlgorithm(\_ _ _ â†’ id) id id

-- The greatest common divisor of a list of numbers `v` is a number `D` such that it is a divisor of all numbers in `v`, and the greatest one of them.
record GreatestCommonDivisor(n : â„•) (v : Vector(n)(â„•)) (D : â„•) : Type{Lvl.ğŸ} where
  constructor intro
  field
    divisor : âˆ€(i) â†’ (D âˆ£ v(i))
    maximum : âˆ€{d} â†’ (âˆ€(i) â†’ (d âˆ£ v(i))) â†’ (d âˆ£ D)

-- `Gcd a b D` is the specialization of `GreatestCommonDivisor` for 2 elements.
-- Example:
--   Divisor(24) = {1,2,3,4,  6,8,   12,      24}
--   Divisor(60) = {1,2,3,4,5,6  ,10,12,15,20,   30,60}
--   24 = 2Â³ â‹… 3Â¹
--   60 = 2Â² â‹… 3Â¹ â‹… 5Â¹
--   Gcd 24 60 = {max(Divisor(24) âˆ© Divisor(60))} = 2Â² â‹… 3Â¹ = 12
--   Divisor of first : 24 / 12 = 2
--   Divisor of second: 60 / 12 = 5
Gcd = GreatestCommonDivisor(2) âˆ˜â‚‚ Vector.pair
module Gcd {a b D} where
  introâ‚‚ : _ â†’ _ â†’ (âˆ€{d} â†’ _ â†’ _ â†’ (d âˆ£ D)) â†’ Gcd a b D
  introâ‚‚ divisorâ‚— divisoráµ£ maximum = intro{2}{Vector.pair a b}
    (\{ğŸ â†’ divisorâ‚— ; (ğ’(ğŸ)) â†’ divisoráµ£})
    (\dv â†’ maximum (dv ğŸ) (dv (ğ’ ğŸ)))
  module _ (inst : Gcd a b D) where
    open GreatestCommonDivisor(inst) public
    divisorâ‚— = divisor ğŸ
    divisoráµ£ = divisor(ğ’ ğŸ)
    maximumâ‚‚ = \{d} a b â†’ maximum{d} \{ğŸ â†’ a ; (ğ’(ğŸ)) â†’ b}
