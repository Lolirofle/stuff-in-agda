module Numeral.Natural.Coprime.Proofs where

open import Data
open import Functional
open import Logic
open import Logic.Classical
open import Logic.Propositional
open import Logic.Propositional.Theorems
import      Lvl
open import Numeral.Finite
open import Numeral.Natural
open import Numeral.Natural.Coprime
open import Numeral.Natural.Function.GreatestCommonDivisor
open import Numeral.Natural.Relation.Divisibility.Decidable
open import Numeral.Natural.Relation.Divisibility.Proofs
open import Numeral.Natural.Oper
open import Numeral.Natural.Prime
open import Numeral.Natural.Prime.Proofs
open import Numeral.Natural.Relation
open import Numeral.Natural.Relation.Divisibility
open import Numeral.Natural.Relation.Order
open import Numeral.Natural.Relation.Order.Proofs
open import Relator.Equals
open import Structure.Relator.Properties
open import Type.Properties.Decidable.Proofs
open import Type

private variable n x y d p : â„•

-- 1 is the only number coprime to itself because it does not have any divisors except for itself.
Coprime-reflexivity-condition : Coprime(n)(n) â†” (n â‰¡ 1)
Coprime-reflexivity-condition {n} = [â†”]-intro l (r{n}) where
  l : Coprime(n)(n) â† (n â‰¡ 1)
  Coprime.proof(l [â‰¡]-intro) {a} a1 _ = [1]-only-divides-[1] (a1)

  r : âˆ€{n} â†’ Coprime(n)(n) â†’ (n â‰¡ 1)
  r {ğŸ}       (intro z1)   = z1 DivğŸ DivğŸ
  r {ğ’(ğŸ)}    _            = [â‰¡]-intro
  r {ğ’(ğ’(n))} (intro ssn1) = ssn1 {ğ’(ğ’(n))} (reflexivity(_âˆ£_)) (reflexivity(_âˆ£_))

instance
  Coprime-symmetry : Symmetry(Coprime)
  Coprime.proof(Symmetry.proof Coprime-symmetry (intro proof)) {n} nx ny = proof {n} ny nx

-- The only number coprime to 0 is 1 because while all numbers divide 0, only 1 divides 1.
Coprime-of-0-condition : âˆ€{x} â†’ Coprime(0)(x) â†’ (x â‰¡ 1)
Coprime-of-0-condition {0}       (intro n1) = n1 DivğŸ DivğŸ
Coprime-of-0-condition {1}       (intro n1) = [â‰¡]-intro
Coprime-of-0-condition {ğ’(ğ’(x))} (intro n1) = n1 DivğŸ (reflexivity(_âˆ£_))

-- 1 is coprime to all numbers because only 1 divides 1.
Coprime-of-1 : Coprime(1)(x)
Coprime.proof (Coprime-of-1 {x}) {n} n1 nx = [1]-only-divides-[1] n1

Coprime-without-operator : âˆ€{_â–«_ : â„• â†’ â„• â†’ â„•} â†’ (âˆ€{n} â†’ (n âˆ£ x) â†’ (n âˆ£ y) â†’ (n âˆ£ (x â–« y))) â†’ Coprime(x)(x â–« y) â†’ Coprime(x)(y)
Coprime.proof (Coprime-without-operator div (intro proof)) nx ny = proof nx (div nx ny)

Coprime-of-operator : âˆ€{_â–«_ : â„• â†’ â„• â†’ â„•} â†’ (âˆ€{n} â†’ (n âˆ£ (x â–« y)) â†’ (n âˆ£ x) â†’ (n âˆ£ y)) â†’ Coprime(x)(y) â†’ Coprime(x)(x â–« y)
Coprime.proof (Coprime-of-operator {x}{y} div (intro proof)) {n} nx nxy = proof {n} nx (div nxy nx)

Coprime-of-[+] : Coprime(x)(y) â†’ Coprime(x)(x + y)
Coprime-of-[+] = Coprime-of-operator{_â–«_ = _+_} ([â†”]-to-[â†’] âˆ˜ divides-without-[+])

Coprime-of-[âˆ’â‚€] : (x â‰¥ y) â†’ Coprime(x)(y) â†’ Coprime(x)(x âˆ’â‚€ y)
Coprime-of-[âˆ’â‚€] ord = Coprime-of-operator{_â–«_ = _âˆ’â‚€_} ([â†”]-to-[â†’] âˆ˜ divides-without-[âˆ’â‚€] ord)

-- Coprimality is obviously equivalent to the greatest common divisor being 1 by definition.
Coprime-gcd : Coprime(x)(y) â†” (gcd(x)(y) â‰¡ 1)
Coprime-gcd = [â†”]-transitivity ([â†”]-intro l r) Gcd-gcd-value where
  l : Coprime(x)(y) â† Gcd(x)(y) 1
  Coprime.proof (l p) nx ny = [1]-only-divides-[1] (Gcd.maximumâ‚‚ p nx ny)

  r : Coprime(x)(y) â†’ Gcd(x)(y) 1
  Gcd.divisor(r (intro coprim)) ğŸ     = [1]-divides
  Gcd.divisor(r (intro coprim)) (ğ’ ğŸ) = [1]-divides
  Gcd.maximum(r (intro coprim)) dv with [â‰¡]-intro â† coprim (dv ğŸ) (dv(ğ’ ğŸ)) = [1]-divides

-- A smaller number and a greater prime number is coprime.
-- If the greater number is prime, then no smaller number will divide it except for 1, and greater numbers never divide smaller ones.
-- Examples (y = 7):
--   The prime factors of 7 is only itself (because it is prime).
--   Then the only alternatives for x are:
--   x âˆˆ {1,2,3,4,5,6}
--   None of them is able to have 7 as a prime factor because it is greater:
--   1=1, 2=2, 3=3, 4=2â‹…2, 5=5, 6=2â‹…3
Coprime-of-Prime : (ğ’(x) < y) â†’ Prime(y) â†’ Coprime(ğ’(x))(y)
Coprime.proof (Coprime-of-Prime (succ(succ lt)) prim) nx ny with prime-only-divisors prim ny
Coprime.proof (Coprime-of-Prime (succ(succ lt)) prim) nx ny | [âˆ¨]-introâ‚— n1        = n1
Coprime.proof (Coprime-of-Prime (succ(succ lt)) prim) nx ny | [âˆ¨]-introáµ£ [â‰¡]-intro with () â† [â‰¤]-to-[â‰¯] lt ([â‰¤]-without-[ğ’] (divides-upper-limit nx))

-- A prime number either divides a number or forms a coprime pair.
-- If a prime number does not divide a number, then it cannot share any divisors because by definition, a prime only has 1 as a divisor.
Prime-to-div-or-coprime : Prime(x) â†’ ((x âˆ£ y) âˆ¨ Coprime(x)(y))
Prime-to-div-or-coprime {y = y} (intro {x} prim) = [Â¬â†’]-disjunctive-formáµ£ â¦ƒ decider-to-classical â¦„ (intro âˆ˜ coprim) where
  coprim : (ğ’(ğ’(x)) âˆ¤ y) â†’ âˆ€{n} â†’ (n âˆ£ ğ’(ğ’(x))) â†’ (n âˆ£ y) â†’ (n â‰¡ 1)
  coprim nxy {ğŸ}   nx ny with () â† [0]-divides-not nx
  coprim nxy {ğ’ n} nx ny with prim nx
  ... | [âˆ¨]-introâ‚— [â‰¡]-intro = [â‰¡]-intro
  ... | [âˆ¨]-introáµ£ [â‰¡]-intro with () â† nxy ny

divides-to-converse-coprimeâ‚— : âˆ€{x y z} â†’ (x âˆ£ y) â†’ Coprime(y)(z) â†’ Coprime(x)(z)
divides-to-converse-coprimeâ‚— xy (intro yz) = intro(nx â†¦ nz â†¦ yz (transitivity(_âˆ£_) nx xy) nz)

divides-to-converse-coprimeáµ£ : âˆ€{x y z} â†’ (x âˆ£ y) â†’ Coprime(z)(y) â†’ Coprime(z)(x)
divides-to-converse-coprimeáµ£ div cop = symmetry(Coprime) (divides-to-converse-coprimeâ‚— div (symmetry(Coprime) cop))

divides-to-converse-coprime : âˆ€{xâ‚ xâ‚‚ yâ‚ yâ‚‚} â†’ (xâ‚ âˆ£ xâ‚‚) â†’ (yâ‚ âˆ£ yâ‚‚) â†’ Coprime(xâ‚‚)(yâ‚‚) â†’ Coprime(xâ‚)(yâ‚)
divides-to-converse-coprime div1 div2 coprim = divides-to-converse-coprimeâ‚— div1 (divides-to-converse-coprimeáµ£ div2 coprim)

Coprime-positive : âˆ€{x y} â†’ Coprime x y â†’ (Positive(x) âˆ¨ Positive(y))
Coprime-positive {ğŸ}   {ğŸ}   coprim with () â† Coprime-of-0-condition coprim
Coprime-positive {ğŸ}   {ğ’ y} coprim = [âˆ¨]-introáµ£ <>
Coprime-positive {ğ’ x} {ğŸ}   coprim = [âˆ¨]-introâ‚— <>
Coprime-positive {ğ’ x} {ğ’ y} coprim = [âˆ¨]-introáµ£ <>
