module Numeral.FixedPositional where

import      Lvl
open import Data using (<>)
open import Data.List
open import Data.Boolean hiding (elim)
open import Data.Boolean.Stmt
open import Numeral.Finite
open import Numeral.Natural
open import Functional
open import Syntax.Number
open import Type

private variable â„“ : Lvl.Level
private variable z : Bool
private variable b n : â„•

-- A formalization of the fixed positional radix numeral system for the notation of numbers.
-- Each number is represented by a list of digits.
-- Digits are a finite set of ordered objects starting with zero (0), and a finite number of its successors.
-- Examples using radix 10 (b = 10):
--   âˆ…                               represents 0
--   # 0                             represents 0
--   # 0 Â· 0                         represents 0
--   # 2                             represents 2
--   # 1 Â· 3                         represents 13
--   # 5 Â· 0 Â· 4 Â· 0 Â· 0             represents 50400
--   # 0 Â· 5 Â· 0 Â· 4 Â· 0 Â· 0         represents 50400
--   # 0 Â· 0 Â· 0 Â· 5 Â· 0 Â· 4 Â· 0 Â· 0 represents 50400
--   # 5 Â· 0 Â· 4 Â· 0 Â· 0 Â· 0         represents 504000
-- Example using radix 2 (b = 2):
--   # 1 Â· 1             represents 3
--   # 0 Â· 1 Â· 0 Â· 1 Â· 1 represents 11
-- Note: The radix is also called base.
-- Note: This representation is in little endian: The deepest digit in the list is the most significant digit (greatest), and the first position of the list is the least significant digit. Note that the (_Â·_) operator is a reversed list cons operator, so it constructs a list from right to left when written without parenthesis.
Positional = List âˆ˜ ğ•Ÿ
pattern # n = n âŠ° âˆ…
pattern _Â·_ a b = b âŠ° a
infixl 100 _Â·_

private variable x y : Positional(b)
private variable d : ğ•Ÿ(n)

-- Two positionals are equal when the sequence of digits in the lists are the same.
-- But also, when there are zeroes at the most significant positions, they should be skipped.
-- Examples:
--   # 4 Â· 5         â‰¡â‚šâ‚’â‚› # 4 Â· 5
--   # 0 Â· 0 Â· 4 Â· 5 â‰¡â‚šâ‚’â‚› # 4 Â· 5
--   # 4 Â· 5         â‰¡â‚šâ‚’â‚› # 0 Â· 0 Â· 4 Â· 5
--   # 0 Â· 0 Â· 4 Â· 5 â‰¡â‚šâ‚’â‚› # 0 Â· 4 Â· 5
data _â‰¡â‚šâ‚’â‚›_ : Positional b â†’ Positional b â†’ Type{Lvl.ğŸ} where
  empty : (_â‰¡â‚šâ‚’â‚›_ {b} âˆ… âˆ…)
  skipâ‚— : (x â‰¡â‚šâ‚’â‚› âˆ…) â†’ (x Â· ğŸ â‰¡â‚šâ‚’â‚› âˆ…)
  skipáµ£ : (âˆ… â‰¡â‚šâ‚’â‚› y) â†’ (âˆ… â‰¡â‚šâ‚’â‚› y Â· ğŸ)
  step  : (x â‰¡â‚šâ‚’â‚› y) â†’ (x Â· d â‰¡â‚šâ‚’â‚› y Â· d)

module _ where
  open import Numeral.Natural.Oper

  -- Converts a positional to a number by adding the first digit and multiplying the rest with the radix.
  -- Examples in radix 10 (b = 10):
  --   to-â„• (# 1 Â· 2 Â· 3) = 10 â‹… (10 â‹… (10 â‹… 0 + 1) + 2) + 3 = ((0 + 100) + 20) + 3 = 100 + 20 + 3 = 123
  --   to-â„• (# a Â· b Â· c) = 10 â‹… (10 â‹… (10 â‹… 0 + a) + b) + c = (10Â² â‹… a) + (10Â¹ â‹… b) + c = (10Â² â‹… a) + (10Â¹ â‹… b) + (10â° â‹… c)
  to-â„• : Positional b â†’ â„•
  to-â„•     âˆ…       = ğŸ
  to-â„• {b} (l Â· n) = (b â‹… (to-â„• l)) + (toâ„• n)

  import      Data.List.Functions as List
  open import Logic.Propositional
  open import Numeral.Finite.Proofs
  open import Numeral.Natural.Inductions
  open import Numeral.Natural.Oper.Comparisons
  open import Numeral.Natural.Oper.FlooredDivision
  open import Numeral.Natural.Oper.FlooredDivision.Proofs
  open import Numeral.Natural.Oper.Modulo
  open import Numeral.Natural.Oper.Modulo.Proofs
  open import Numeral.Natural.Relation.Order
  open import Numeral.Natural.Relation.Order.Decidable
  open import Numeral.Natural.Relation.Order.Proofs
  open import Structure.Relator.Ordering
  import      Structure.Relator.Names as Names
  open import Structure.Relator.Equivalence
  open import Structure.Relator.Properties
  open import Syntax.Transitivity
  open import Type.Properties.Decidable
  open import Type.Properties.Decidable.Proofs

  -- Converts a number to its positional representation in the specified radix.
  -- This is done by extracting the next digit using modulo of the radix and then dividing the rest.
  -- This is the inverse of to-â„•.
  from-â„•-rec : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ (x : â„•) â†’ ((prev : â„•) â¦ƒ _ : prev < x â¦„ â†’ Positional(b)) â†’ Positional(b)
  from-â„•-rec b@{ğ’(ğ’ _)} ğŸ       _    = âˆ…
  from-â„•-rec b@{ğ’(ğ’ _)} n@(ğ’ _) prev = (prev(n âŒŠ/âŒ‹ b) â¦ƒ [âŒŠ/âŒ‹]-ltâ‚— {n}{b} â¦„) Â· (fromâ„• (n mod b) â¦ƒ [â†”]-to-[â†’] decider-true (mod-maxáµ£{n}{b}) â¦„)
  from-â„• : â„• â†’ Positional(b)
  from-â„• {0}        = const âˆ…
  from-â„• {1}        = List.repeat ğŸ
  from-â„• b@{ğ’(ğ’ _)} = Strict.Properties.wellfounded-recursion(_<_) from-â„•-rec

  instance
    [â‰¡â‚šâ‚’â‚›]-reflexivity : Reflexivity(_â‰¡â‚šâ‚’â‚›_ {b})
    [â‰¡â‚šâ‚’â‚›]-reflexivity = intro p where
      p : Names.Reflexivity(_â‰¡â‚šâ‚’â‚›_ {b})
      p {x = âˆ…}     = empty
      p {x = _ âŠ° _} = _â‰¡â‚šâ‚’â‚›_.step p

  instance
    [â‰¡â‚šâ‚’â‚›]-symmetry : Symmetry(_â‰¡â‚šâ‚’â‚›_ {b})
    [â‰¡â‚šâ‚’â‚›]-symmetry = intro p where
      p : Names.Symmetry(_â‰¡â‚šâ‚’â‚›_ {b})
      p empty            = empty
      p (skipâ‚— eq)       = skipáµ£ (p eq)
      p (skipáµ£ eq)       = skipâ‚— (p eq)
      p (_â‰¡â‚šâ‚’â‚›_.step eq) = _â‰¡â‚šâ‚’â‚›_.step (p eq)

  instance
    [â‰¡â‚šâ‚’â‚›]-transitivity : Transitivity(_â‰¡â‚šâ‚’â‚›_ {b})
    [â‰¡â‚šâ‚’â‚›]-transitivity = intro p where
      p : Names.Transitivity(_â‰¡â‚šâ‚’â‚›_ {b})
      p empty           empty           = empty
      p empty           (skipáµ£ b)       = skipáµ£ b
      p (skipâ‚— a)       empty           = skipâ‚— a
      p (skipâ‚— a)       (skipáµ£ b)       = _â‰¡â‚šâ‚’â‚›_.step (p a b)
      p (skipáµ£ a)       (skipâ‚— b)       = p a b
      p (skipáµ£ a)       (_â‰¡â‚šâ‚’â‚›_.step b) = skipáµ£ (p a b)
      p (_â‰¡â‚šâ‚’â‚›_.step a) (skipâ‚— b)       = skipâ‚— (p a b)
      p (_â‰¡â‚šâ‚’â‚›_.step a) (_â‰¡â‚šâ‚’â‚›_.step b) = _â‰¡â‚šâ‚’â‚›_.step (p a b)

  instance
    [â‰¡â‚šâ‚’â‚›]-equivalence : Equivalence(_â‰¡â‚šâ‚’â‚›_ {b})
    [â‰¡â‚šâ‚’â‚›]-equivalence = intro

  open import Structure.Setoid using (Equiv ; intro)

  Positional-equiv : Equiv(Positional(b))
  Positional-equiv {b} = intro _ â¦ƒ [â‰¡â‚šâ‚’â‚›]-equivalence {b} â¦„

  open import Functional.Instance
  open import Numeral.Natural.Relation.Proofs
  open import Structure.Function
  open import Structure.Operator
  open import Relator.Equals
  open import Relator.Equals.Proofs

  from-â„•-digit : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ â¦ƒ _ : IsTrue(n <? b) â¦„ â†’ (from-â„• {b} n â‰¡â‚šâ‚’â‚› fromâ„• n âŠ° âˆ…)
  from-â„•-digit b@{ğ’(ğ’ bb)} {n} = Strict.Properties.wellfounded-recursion-intro(_<_) {rec = from-â„•-rec} {Ï† = \{n} expr â†’ â¦ƒ _ : IsTrue(n <? b) â¦„ â†’ (expr â‰¡â‚šâ‚’â‚› (fromâ„• n âŠ° âˆ…))} p {n} where
    p : (y : â„•) â†’ _ â†’ _ â†’ â¦ƒ _ : IsTrue(y <? b) â¦„ â†’ (from-â„• y â‰¡â‚šâ‚’â‚› (fromâ„• y âŠ° âˆ…))
    p ğŸ prev eq = skipáµ£ empty
    p (ğ’ y) prev eq â¦ƒ ord â¦„ =
      from-â„• (ğ’(y))                                                         ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ subâ‚‚(_â‰¡_)(_â‰¡â‚šâ‚’â‚›_) eq ]
      from-â„• (ğ’(y) âŒŠ/âŒ‹ b) Â· fromâ„• (ğ’(y) mod b) â¦ƒ yb-ord? â¦„                 ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ _â‰¡â‚šâ‚’â‚›_.step (prev â¦ƒ [âŒŠ/âŒ‹]-ltâ‚—{ğ’ y}{b}  â¦„ â¦ƒ div-ord â¦„) ]
      âˆ… Â· fromâ„• (ğ’(y) âŒŠ/âŒ‹ b) â¦ƒ div-ord â¦„ Â· fromâ„• (ğ’(y) mod b) â¦ƒ yb-ord? â¦„ ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ subâ‚‚(_â‰¡_)(_â‰¡â‚šâ‚’â‚›_) (congruenceâ‚‚-â‚‚(_Â·_)(âˆ… Â· fromâ„• (ğ’(y) âŒŠ/âŒ‹ b) â¦ƒ div-ord â¦„) (congruence-fromâ„• {x = ğ’(y) mod b} â¦ƒ yb-ord? â¦„ {y = ğ’(y)} â¦ƒ ord â¦„ (mod-lesser-than-modulus {ğ’ y}{b} yb-ord))) ]
      âˆ… Â· fromâ„• (ğ’(y) âŒŠ/âŒ‹ b) â¦ƒ div-ord â¦„ Â· fromâ„• (ğ’(y))                   ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ _â‰¡â‚šâ‚’â‚›_.step (subâ‚‚(_â‰¡_)(_â‰¡â‚šâ‚’â‚›_) (congruenceâ‚‚-â‚‚(_Â·_)(_) (congruence-fromâ„• â¦ƒ infer â¦„ â¦ƒ div-ord â¦„ ([âŒŠ/âŒ‹]-zero {ğ’ y}{b} yb-ord2)))) ]
      âˆ… Â· ğŸ Â· fromâ„• (ğ’(y))                                                 ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ _â‰¡â‚šâ‚’â‚›_.step (skipâ‚— empty) ]
      âˆ… Â· fromâ„• (ğ’(y))                                                     ğŸ–-end
      where
        yb-ord? = [â†”]-to-[â†’] decider-true (mod-maxáµ£ {ğ’(y)}{b} â¦ƒ infer â¦„)
        yb-ord = [â†”]-to-[â†] (decider-true â¦ƒ [<]-decider â¦„) ord
        yb-ord2 = [â†”]-to-[â†] (decider-true â¦ƒ [<]-decider â¦„) ord
        div-ord = [â†”]-to-[â†’] (decider-true â¦ƒ [<]-decider â¦„) (subtransitivityâ‚—(_<_)(_â‰¤_) ([âŒŠ/âŒ‹]-leâ‚— {b = b}) yb-ord2)

  from-â„•-step : â¦ƒ b-size : IsTrue(b >? 1) â¦„
              â†’ let pos = [â†”]-to-[â†] Positive-greater-than-zero ([â‰¤]-predecessor ([â†”]-to-[â†] (decider-true â¦ƒ [<]-decider {1}{b} â¦„) b-size))
                in (from-â„• {b} n â‰¡â‚šâ‚’â‚› (from-â„• {b} ((n âŒŠ/âŒ‹ b) â¦ƒ pos â¦„)) Â· (fromâ„• ((n mod b) â¦ƒ pos â¦„) â¦ƒ [â†”]-to-[â†’] decider-true (mod-maxáµ£{n}{b} â¦ƒ pos â¦„) â¦„))
  from-â„•-step b@{ğ’(ğ’ bb)} {n} = Strict.Properties.wellfounded-recursion-intro(_<_) {rec = from-â„•-rec} {Ï† = \{n} expr â†’ (expr â‰¡â‚šâ‚’â‚› from-â„• {b} (n âŒŠ/âŒ‹ b) Â· (fromâ„• (n mod b) â¦ƒ ord n â¦„))} p {n} where
    ord = \n â†’ [â†”]-to-[â†’] decider-true (mod-maxáµ£{n}{b})
    p : (y : â„•) â†’ _ â†’ _ â†’ Strict.Properties.accessible-recursion(_<_) from-â„•-rec y â‰¡â‚šâ‚’â‚› from-â„• (y âŒŠ/âŒ‹ b) Â· fromâ„• (y mod b) â¦ƒ ord y â¦„
    p ğŸ     prev eq = skipáµ£ empty
    p (ğ’ y) prev eq = (subâ‚‚(_â‰¡_)(_â‰¡â‚šâ‚’â‚›_) eq)

  open import Numeral.Natural.Oper.FlooredDivision.Proofs.Compatibility
  open import Numeral.Natural.Oper.FlooredDivision.Proofs.Inverse
  open import Numeral.Natural.Oper.Proofs
  open import Numeral.Natural.Relation.Divisibility.Proofs
  open import Structure.Operator.Properties
  from-â„•-step-invs : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ (from-â„• {b} ((b â‹… n) + (toâ„• d)) â‰¡â‚šâ‚’â‚› (from-â„• {b} n) Â· d)
  from-â„•-step-invs b@{ğ’(ğ’ bb)} {n}{d} =
    from-â„• (b â‹… n + toâ„• d)                                                     ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ from-â„•-step {n = b â‹… n + toâ„• d} ]
    from-â„• ((b â‹… n + toâ„• d) âŒŠ/âŒ‹ b) Â· (fromâ„• ((b â‹… n + toâ„• d) mod b) â¦ƒ _ â¦„) ğŸ–[ _â‰¡â‚šâ‚’â‚›_ ]-[ subâ‚‚(_â‰¡_)(_â‰¡â‚šâ‚’â‚›_) (congruenceâ‚‚(_Â·_) (congruenceâ‚(from-â„•) r) (congruence-fromâ„• â¦ƒ infer â¦„ â¦ƒ ord1 â¦„ â¦ƒ ord2 â¦„ q ğŸ– â„•-ğ•Ÿ-inverse)) ]
    from-â„• n Â· d                                                                  ğŸ–-end where
      ord1 = [â†”]-to-[â†’] decider-true (mod-maxáµ£{(b â‹… n) + (toâ„• d)}{b})
      ord2 = [â†”]-to-[â†’] decider-true (toâ„•-bounded {b}{d})
      q =
        ((b â‹… n) + toâ„• d) mod b ğŸ–[ _â‰¡_ ]-[ congruenceâ‚(_mod b) (commutativity(_+_) {b â‹… n}{toâ„• d}) ]
        (toâ„• d + (b â‹… n)) mod b ğŸ–[ _â‰¡_ ]-[ mod-of-modulus-sum-multiple {toâ„• d}{b}{n} ]
        (toâ„• d) mod b           ğŸ–[ _â‰¡_ ]-[ mod-lesser-than-modulus ([â‰¤]-without-[ğ’] toâ„•-bounded) ]
        toâ„• d                   ğŸ–-end
      r =
        (b â‹… n + toâ„• d) âŒŠ/âŒ‹ b             ğŸ–[ _â‰¡_ ]-[ [âŒŠ/âŒ‹][+]-distributivityáµ£ {b â‹… n}{toâ„• d}{b} ([âˆ¨]-introâ‚— (DivN n)) ]
        ((b â‹… n) âŒŠ/âŒ‹ b) + ((toâ„• d) âŒŠ/âŒ‹ b) ğŸ–[ _â‰¡_ ]-[ congruenceâ‚‚(_+_) ([âŒŠ/âŒ‹][swapâ‹…]-inverseOperatoráµ£ {b}{n}) ([âŒŠ/âŒ‹]-zero (toâ„•-bounded {b}{d})) ]
        n + ğŸ                                ğŸ–[ _â‰¡_ ]-[]
        n                                    ğŸ–-end

  open import Numeral.Natural.Oper.DivMod.Proofs
  open import Structure.Function.Domain
  import      Structure.Function.Names as Names

  instance
    from-to-inverse : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ Inverseáµ£ â¦ƒ Positional-equiv{b} â¦„ from-â„• to-â„•
    from-to-inverse b@{ğ’(ğ’ _)} = intro p where
      p : Names.Inverses â¦ƒ Positional-equiv{b} â¦„ from-â„• to-â„•
      p{x = âˆ…}     = empty
      p{x = x Â· n} = from-â„•-step-invs{b}{to-â„• x}{n} ğŸ– _â‰¡â‚šâ‚’â‚›_.step (p{x = x})

  instance
    to-from-inverse : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ Inverseáµ£(to-â„•{b}) (from-â„•{b})
    to-from-inverse {b@(ğ’(ğ’ bb))} = intro (\{n} â†’ Strict.Properties.wellfounded-recursion-intro(_<_) {rec = from-â„•-rec {b}} {Ï† = \{n} expr â†’ (to-â„• expr â‰¡ n)} p {n}) where
      p : (y : â„•) â†’ _ â†’ _ â†’ (to-â„• {b} (from-â„• {b} y) â‰¡ y)
      p ğŸ     _    _  = [â‰¡]-intro
      p (ğ’ y) prev eq =
        to-â„• {b} (from-â„• (ğ’ y))                                                       ğŸ–[ _â‰¡_ ]-[ congruenceâ‚(to-â„•) eq ]
        to-â„• {b} ((from-â„• (ğ’(y) âŒŠ/âŒ‹ b)) Â· (fromâ„• (ğ’(y) mod b) â¦ƒ _ â¦„))                ğŸ–[ _â‰¡_ ]-[]
        (b â‹… to-â„• {b} (from-â„• {b} (ğ’(y) âŒŠ/âŒ‹ b))) + toâ„• (fromâ„• (ğ’(y) mod b) â¦ƒ _ â¦„) ğŸ–[ _â‰¡_ ]-[ congruenceâ‚‚(_+_) (congruenceâ‚‚-â‚‚(_â‹…_)(b) (prev{ğ’(y) âŒŠ/âŒ‹ b} â¦ƒ ord2 â¦„)) (ğ•Ÿ-â„•-inverse {b}{ğ’(y) mod b} â¦ƒ ord1 â¦„) ]
        (b â‹… (ğ’(y) âŒŠ/âŒ‹ b)) + (ğ’(y) mod b)                                             ğŸ–[ _â‰¡_ ]-[ [âŒŠ/âŒ‹][mod]-is-division-with-remainder-pred-commuted {ğ’ y}{b} ]
        ğ’(y)                                                                          ğŸ–-end
        where
          ord1 = [â†”]-to-[â†’] decider-true (mod-maxáµ£{ğ’(y)}{b})
          ord2 = [âŒŠ/âŒ‹]-ltâ‚— {ğ’ y}{b}

  instance
    to-â„•-function : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ Function â¦ƒ Positional-equiv â¦„ â¦ƒ [â‰¡]-equiv â¦„ (to-â„• {b})
    to-â„•-function {b} = intro p where
      p : Names.Congruenceâ‚ â¦ƒ Positional-equiv â¦„ â¦ƒ [â‰¡]-equiv â¦„ (to-â„• {b})
      p empty                          = reflexivity(_â‰¡_)
      p (skipâ‚— xy) rewrite p xy        = reflexivity(_â‰¡_)
      p (skipáµ£ {y = âˆ…} xy)             = reflexivity(_â‰¡_)
      p (skipáµ£ {y = ğŸ âŠ° y} (skipáµ£ xy))
        rewrite symmetry(_â‰¡_) (p xy)   = reflexivity(_â‰¡_)
      p (_â‰¡â‚šâ‚’â‚›_.step xy)
        rewrite p xy = reflexivity(_â‰¡_)

  open import Logic.Predicate
  open import Structure.Function.Domain.Proofs

  instance
    from-â„•-bijective : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ Bijective â¦ƒ [â‰¡]-equiv â¦„ â¦ƒ Positional-equiv â¦„ (from-â„• {b})
    from-â„•-bijective = [â†”]-to-[â†’] (invertible-when-bijective â¦ƒ _ â¦„ â¦ƒ _ â¦„) ([âˆƒ]-intro to-â„• â¦ƒ [âˆ§]-intro infer ([âˆ§]-intro infer infer) â¦„)

  instance
    to-â„•-bijective : â¦ƒ b-size : IsTrue(b >? 1) â¦„ â†’ Bijective â¦ƒ Positional-equiv â¦„ â¦ƒ [â‰¡]-equiv â¦„ (to-â„• {b})
    to-â„•-bijective = [â†”]-to-[â†’] (invertible-when-bijective â¦ƒ _ â¦„ â¦ƒ _ â¦„) ([âˆƒ]-intro from-â„• â¦ƒ [âˆ§]-intro ([â‰¡]-to-function â¦ƒ Positional-equiv â¦„) ([âˆ§]-intro infer infer) â¦„)

  import      Data.Option.Functions as Option
  open import Function.Names
  open import Numeral.Natural.Relation.Order.Proofs

  -- TODO: Trying to define a bijection, but not really possible because not all functions
  PositionalSequence : List(ğ•Ÿ(ğ’ b)) â†’ (â„• â†’ ğ•Ÿ(ğ’ b))
  PositionalSequence l n = (List.indexâ‚€ n l) Option.or ğŸ

  sequencePositional : (f : â„• â†’ ğ•Ÿ(ğ’ b)) â†’ âˆƒ(N â†¦ (f âˆ˜ (_+ N) âŠœ const ğŸ)) â†’ List(ğ•Ÿ(ğ’ b))
  sequencePositional f ([âˆƒ]-intro ğŸ)           = âˆ…
  sequencePositional f ([âˆƒ]-intro (ğ’ N) â¦ƒ p â¦„) = f(ğŸ) âŠ° sequencePositional (f âˆ˜ ğ’) ([âˆƒ]-intro N â¦ƒ \{n} â†’ p{n} â¦„)
