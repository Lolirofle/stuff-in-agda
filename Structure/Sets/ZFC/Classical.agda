open import Type
open import Structure.Relator
open import Structure.Setoid renaming (_â‰¡_ to _â‰¡â‚‘_)

module Structure.Sets.ZFC.Classical {â„“â‚› â„“â‚— â„“â‚‘} {S : Type{â„“â‚›}} â¦ƒ equiv : Equiv{â„“â‚‘}(S) â¦„ (_âˆˆ_ : S â†’ S â†’ Type{â„“â‚—}) â¦ƒ [âˆˆ]-binaryRelator : BinaryRelator(_âˆˆ_) â¦„ where

open import Data.Either as Either using ()
open import Functional
open import Logic.Classical
open import Logic.Predicate
open import Logic.Propositional
open import Logic.Propositional.Theorems
import      Lvl
open import Structure.Operator
open import Structure.Relator.Proofs
open import Structure.Relator.Properties
import      Structure.Sets.Names
open        Structure.Sets.Names.From-[âˆˆ] (_âˆˆ_)
open        Structure.Sets.Names.Relations (_âˆˆ_)
open import Structure.Sets.ZFC(_âˆˆ_) â¦ƒ [âˆˆ]-binaryRelator â¦„
open import Syntax.Function
open import Syntax.Implication
open import Syntax.Transitivity

private variable â„“ : Lvl.Level
private variable T A B : Type{â„“}
private variable x : S

module _ â¦ƒ zfc : ZFC â¦„ where
  open ZFC(zfc)

  -- ZFC implies excluded middle.
  -- Note that this only requires the following set related axioms:
  -- â€¢ Set extensionality (Not really neccessary if every equality is replaced with set equality instead).
  -- â€¢ Axiom of choice (TODO: Is this really neccessary? The proof only uses the choice function on finite sets? Further investigation on choice functions for finite sets would clear this up).
  -- â€¢ Choice functions are functions.
  -- â€¢ Restricted set comprehension.
  -- â€¢ Existence of at least two different sets (In this proof, âˆ… and {âˆ…} (ğ‘‡ and ğ¹) is used).
  -- â€¢ Existence of a set containing the two different sets, and the existence of all subsets of this set.
  -- Also called: Diaconescu's theorem, Goodmanâ€“Myhill theorem.
  excluded-middle-by-choice : âˆ€{P : Type{â„“}} â†’ Classical(P)
  excluded-middle-by-choice{P = P} = intro $áµ¢â‚™â‚›â‚œ
    let
      instance
        pos-rel : UnaryRelator(x â†¦ P âˆ¨ (x â‰¡ ğ‘‡))
        pos-rel = [âˆ¨]-unaryRelator â¦ƒ rel-Q = binary-unaryRelatoráµ£ â¦„

      instance
        neg-rel : UnaryRelator(x â†¦ P âˆ¨ (x â‰¡ ğ¹))
        neg-rel = [âˆ¨]-unaryRelator â¦ƒ rel-Q = binary-unaryRelatoráµ£ â¦„

      pos = filter (x â†¦ P âˆ¨ (x â‰¡ ğ‘‡)) BoolSet
      neg = filter (x â†¦ P âˆ¨ (x â‰¡ ğ¹)) BoolSet

      -- The contents of pos by definition. A subset of BoolSet which is equal to BoolSet when P, {ğ‘‡} otherwise.
      pos-containment : (x âˆˆ pos) â†” (x âˆˆ BoolSet) âˆ§ (P âˆ¨ (x â‰¡ ğ‘‡))
      pos-containment = restricted-comprehension

      -- The contents of neg by definition. A subset of BoolSet which is equal to BoolSet when P, {ğ¹} otherwise.
      neg-containment : (x âˆˆ neg) â†” (x âˆˆ BoolSet) âˆ§ (P âˆ¨ (x â‰¡ ğ¹))
      neg-containment = restricted-comprehension

      -- pos is non-empty because it should at least contain ğ‘‡ from filtering BoolSet.
      instance
        ne-pos : NonEmpty(pos)
        ne-pos = [â†”]-to-[â†] (nonEmpty-filter) ([âˆƒ]-intro ğ‘‡ â¦ƒ [âˆ§]-intro ğ‘‡-in-BoolSet ([âˆ¨]-introáµ£ (\{_} â†’ [â†”]-reflexivity)) â¦„)

      -- neg is non-empty because it should at least contain ğ¹ from filtering BoolSet.
      instance
        ne-neg : NonEmpty(neg)
        ne-neg = [â†”]-to-[â†] (nonEmpty-filter) ([âˆƒ]-intro ğ¹ â¦ƒ [âˆ§]-intro ğ¹-in-BoolSet ([âˆ¨]-introáµ£ (\{_} â†’ [â†”]-reflexivity)) â¦„)

      -- Chooses an element in respective sets pos and neg.
      pos-choose = choose (pair pos neg) pos
      neg-choose = choose (pair pos neg) neg

      -- By definition of pos, either P holds or pos-choose have to be ğ‘‡.
      pos-choice : P âˆ¨ (pos-choose â‰¡ ğ‘‡)
      pos-choice = [âˆ§]-elimáµ£ ([â†”]-to-[â†’] pos-containment (choice {pair pos neg} {pos} â¦ƒ ne-pos â¦„ â¦ƒ pair-contains-left â¦„))

      -- By definition of neg, either P holds or neg-choose have to be ğ¹.
      neg-choice : P âˆ¨ (neg-choose â‰¡ ğ¹)
      neg-choice = [âˆ§]-elimáµ£ ([â†”]-to-[â†’] neg-containment (choice {pair pos neg} {neg} â¦ƒ ne-neg â¦„ â¦ƒ pair-contains-right â¦„))
    in
      â€¢ ( -- Contrapositive of the argument below states that if pos-choose and neg-choose is inequal, then (Â¬ P)
        (_â‡’ -- When P holds, both pos and neg is BoolSet, so they are equal. The pos-choose and neg-choose is the choice function applied to the equal sets pos and neg respectively, and because choose is a function (it respects equality, specifically set equality), pos-choose and neg-choose is also equal.
          P                          â‡’-[ (\p {x} â†’ [â†”]-transitivity (pos-containment {x}) ([â†”]-transitivity ([âˆ§]-mapáµ£-[â†”] ([â†”]-intro (const([âˆ¨]-introâ‚— p)) (const([âˆ¨]-introâ‚— p)))) ([â†”]-symmetry (neg-containment {x})))) ]
          (pos â‰¡ neg)                â‡’-[ [â†”]-to-[â†] set-extensionality ]
          (pos â‰¡â‚‘ neg)               â‡’-[ congruenceâ‚‚(choose) (reflexivity(_â‰¡â‚‘_)) ]
          (pos-choose â‰¡â‚‘ neg-choose) â‡’-end
        ) â‡’
        (P â†’ (pos-choose â‰¡â‚‘ neg-choose))    â‡’-[ contrapositiveáµ£ ]
        ((Â¬ P) â† (pos-choose â‰¢ neg-choose)) â‡’-end
      )
      â€¢ ( -- The case other than P is that pos and neg only contains ğ‘‡ and ğ¹ respectively. This forces pos-choose and neg-choose to be ğ‘‡ and ğ¹ respectively, which means that they are inequal.
        â€¢ pos-choice
        â€¢ neg-choice
        â‡’â‚‚-[ [âˆ§]-intro ]
        (P âˆ¨ (pos-choose â‰¡ ğ‘‡)) âˆ§ (P âˆ¨ (neg-choose â‰¡ ğ¹)) â‡’-[ [â†”]-to-[â†] [âˆ¨][âˆ§]-distributivityâ‚— ]
        P âˆ¨ ((pos-choose â‰¡ ğ‘‡) âˆ§ (neg-choose â‰¡ ğ¹))       â‡’-[ Either.mapRight (\{([âˆ§]-intro p0 n1) â†’ [â‰¡][â‰¢]-semitransitivityáµ£([â‰¡][â‰¢]-semitransitivityâ‚— ([â†”]-to-[â†] set-extensionality p0) zero-one-ineq) (symmetry(_â‰¡â‚‘_) ([â†”]-to-[â†] set-extensionality n1))}) ]
        P âˆ¨ (pos-choose â‰¢ neg-choose)                   â‡’-end
      )
      â‡’â‚‚-[ Either.mapRight ]
      (P âˆ¨ (Â¬ P)) â‡’-end
