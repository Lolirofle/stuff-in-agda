open import Structure.Setoid
open import Structure.Category
open import Structure.Category.Monad
open import Type

module Structure.Category.Monad.Category
  {â„“â‚’ â„“â‚˜}
  {Obj : Type{â„“â‚’}}
  {Morphism : Obj â†’ Obj â†’ Type{â„“â‚˜}}
  â¦ƒ morphism-equiv : âˆ€{x y : Obj} â†’ Equiv(Morphism x y) â¦„
  {cat : Category(Morphism)}
  {T : Obj â†’ Obj}
  (monad : Monad{cat = cat}(T))
  where

open import Data.Tuple as Tuple using ()
open import Functional.Dependent using () renaming (_âˆ˜_ to _âˆ˜á¶ â¿_)
import      Function.Equals
open        Function.Equals.Dependent
open import Logic.Predicate
open import Structure.Category.Functor
open import Structure.Category.Functor.Functors
open import Structure.Category.Monad.ExtensionSystem {cat = cat}
open import Structure.Category.NaturalTransformation
open import Structure.Category.NaturalTransformation.NaturalTransformations
open import Structure.Category.Proofs
open import Structure.Categorical.Properties
open import Structure.Operator
open import Structure.Relator.Equivalence
open import Structure.Relator.Properties
open import Syntax.Transitivity

open Category.ArrowNotation(cat)
open Category(cat)
open Components(cat)(cat)
open Monad(monad)
open Morphism.OperModule{Morphism = Morphism}(_âˆ˜_)
open Morphism.OperModule.IdModule{Morphism = Morphism}(_âˆ˜_)(id)
open Functor(functor)
private instance _ = cat
private open module MorphismEquiv {x}{y} = Equivalence (Equiv-equivalence â¦ƒ morphism-equiv{x}{y} â¦„) using ()

-- Extension operator
-- Also called: _*
ext : âˆ€{x y} â†’ (x âŸ¶ T(y)) â†’ (T(x) âŸ¶ T(y))
ext {x}{y} f = Î¼(y) âˆ˜ map(f)

extension-system : ExtensionSystem(T)
ExtensionSystem.Î· extension-system = Î·
ExtensionSystem.ext extension-system = ext
Function.congruence (ExtensionSystem.ext-function extension-system) xy = congruenceâ‚‚áµ£(_âˆ˜_)(_) (congruenceâ‚(map) xy)
ExtensionSystem.ext-inverse extension-system = _âŠœ_.proof Î¼-functor-[âˆ˜]-identityâ‚—
ExtensionSystem.ext-identity extension-system {x} {y} {f} =
  ext(f) âˆ˜ Î·(x)          ğŸ–[ _â‰¡_ ]-[]
  (Î¼(y) âˆ˜ map(f)) âˆ˜ Î·(x) ğŸ–-[ Morphism.associativity(_âˆ˜_) ]
  Î¼(y) âˆ˜ (map(f) âˆ˜ Î·(x)) ğŸ–-[ congruenceâ‚‚áµ£(_âˆ˜_)(_) (symmetry(_â‰¡_) Î·-natural) ]
  Î¼(y) âˆ˜ (Î·(T(y)) âˆ˜ f)   ğŸ–-[ symmetry(_â‰¡_) (Morphism.associativity(_âˆ˜_)) ]
  (Î¼(y) âˆ˜ Î·(T(y))) âˆ˜ f   ğŸ–-[ congruenceâ‚‚â‚—(_âˆ˜_)(_) (_âŠœ_.proof Î¼-functor-[âˆ˜]-identityáµ£) ]
  id âˆ˜ f                 ğŸ–-[ Morphism.identityâ‚—(_âˆ˜_)(id) ]
  f                      ğŸ–-end
ExtensionSystem.ext-distribute extension-system {x} {y} {z} {f} {g} =
  ext(ext(f) âˆ˜ g)                               ğŸ–[ _â‰¡_ ]-[]
  Î¼(z) âˆ˜ map((Î¼(z) âˆ˜ map(f)) âˆ˜ g)               ğŸ–-[ congruenceâ‚‚áµ£(_âˆ˜_)(_) op-preserving ]
  Î¼(z) âˆ˜ (map(Î¼(z) âˆ˜ map(f)) âˆ˜ map(g))          ğŸ–-[ congruenceâ‚‚áµ£(_âˆ˜_)(_) (congruenceâ‚‚â‚—(_âˆ˜_)(_) op-preserving) ]
  Î¼(z) âˆ˜ ((map(Î¼(z)) âˆ˜ map(map(f))) âˆ˜ map(g))   ğŸ–-[ associate4-231-123(cat) ]
  ((Î¼(z) âˆ˜ map(Î¼(z))) âˆ˜ map(map(f))) âˆ˜ map(g)   ğŸ–-[  congruenceâ‚‚â‚—(_âˆ˜_)(_) ( congruenceâ‚‚â‚—(_âˆ˜_)(_) (_âŠœ_.proof Î¼-functor-[âˆ˜]-commutativity)) ]
  ((Î¼(z) âˆ˜ Î¼(T(z))) âˆ˜ map(map(f))) âˆ˜ map(g)     ğŸ–-[ associate4-123-213(cat) ]
  (Î¼(z) âˆ˜ (Î¼(T(z)) âˆ˜ map(map(f)))) âˆ˜ map(g)     ğŸ–-[ congruenceâ‚‚â‚—(_âˆ˜_)(_) (congruenceâ‚‚áµ£(_âˆ˜_)(_) (NaturalTransformation.natural([âˆƒ]-proof Îœ))) ]
  (Î¼(z) âˆ˜ (map(f) âˆ˜ Î¼(y))) âˆ˜ map(g)             ğŸ–-[ associate4-213-121(cat) ]
  (Î¼(z) âˆ˜ map(f)) âˆ˜ (Î¼(y) âˆ˜ map(g))             ğŸ–[ _â‰¡_ ]-[]
  ext(f) âˆ˜ ext(g)                               ğŸ–-end

-- Also called: Kleisli category
monad-category : Category(\x y â†’ (x âŸ¶ T(y)))
Category._âˆ˜_ monad-category f g = ext(f) âˆ˜ g
Category.id monad-category {x} = Î·(x)
BinaryOperator.congruence (Category.binaryOperator monad-category {x}{y}{z}) {fâ‚}{fâ‚‚} fâ‚fâ‚‚ {gâ‚}{gâ‚‚} gâ‚gâ‚‚ =
  ext(fâ‚) âˆ˜ gâ‚         ğŸ–[ _â‰¡_ ]-[]
  (Î¼(z) âˆ˜ map fâ‚) âˆ˜ gâ‚ ğŸ–-[ Morphism.associativity(_âˆ˜_) ]
  Î¼(z) âˆ˜ (map fâ‚ âˆ˜ gâ‚) ğŸ–-[ congruenceâ‚‚áµ£(_âˆ˜_)(Î¼(z)) (congruenceâ‚‚(_âˆ˜_) (congruenceâ‚(map) fâ‚fâ‚‚) gâ‚gâ‚‚) ]
  Î¼(z) âˆ˜ (map fâ‚‚ âˆ˜ gâ‚‚) ğŸ–-[ Morphism.associativity(_âˆ˜_) ]-sym
  (Î¼(z) âˆ˜ map fâ‚‚) âˆ˜ gâ‚‚ ğŸ–[ _â‰¡_ ]-[]
  ext(fâ‚‚) âˆ˜ gâ‚‚         ğŸ–-end
Morphism.Associativity.proof (Category.associativity monad-category) {x} {y} {z} {w} {f} {g} {h} =
  ext(ext(f) âˆ˜ g) âˆ˜ h   ğŸ–-[ congruenceâ‚‚â‚—(_âˆ˜_)(_) (ExtensionSystem.ext-distribute extension-system) ]
  (ext(f) âˆ˜ ext(g)) âˆ˜ h ğŸ–-[ Morphism.associativity(_âˆ˜_) ]
  ext(f) âˆ˜ (ext(g) âˆ˜ h) ğŸ–-end
Morphism.Identityâ‚—.proof (Tuple.left (Category.identity monad-category)) {x} {y} {f} =
  ext(Î·(y)) âˆ˜ f          ğŸ–[ _â‰¡_ ]-[]
  (Î¼(y) âˆ˜ map(Î·(y))) âˆ˜ f ğŸ–-[ congruenceâ‚‚â‚—(_âˆ˜_)(f) (_âŠœ_.proof Î¼-functor-[âˆ˜]-identityâ‚—) ]
  id âˆ˜ f                 ğŸ–-[ Morphism.identityâ‚—(_âˆ˜_)(id) ]
  f                      ğŸ–-end
Morphism.Identityáµ£.proof (Tuple.right (Category.identity monad-category)) {x} {y} {f} = ExtensionSystem.ext-identity extension-system
