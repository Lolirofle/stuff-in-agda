module Formalization.ClassicalPropositionalLogic.Semantics.Proofs where

import      Lvl
open import Data
open import Data.Boolean
open import Data.Either as Either using (_â€–_ ; Left ; Right)
open import Formalization.ClassicalPropositionalLogic.Syntax
open import Formalization.ClassicalPropositionalLogic.Semantics
open import Functional
open import Logic.Classical     as Logic using (Classical)
import      Logic.Propositional as Logic
open import Logic.Propositional.Equiv
import      Logic.Predicate     as Logic
open import Relator.Equals
open import Relator.Equals.Proofs
open import Sets.PredicateSet using (PredSet ; _âˆˆ_ ; _âˆ‰_ ; _âˆª_ ; _âˆªâ€¢_ ; _âˆ–_ ; _âŠ†_ ; _âŠ‡_ ; âˆ… ; [â‰¡]-to-[âŠ†] ; [â‰¡]-to-[âŠ‡]) renaming (â€¢_ to singleton ; _â‰¡_ to _â‰¡â‚›_)
open import Structure.Relator
open import Type

private variable â„“â‚š â„“ : Lvl.Level
private variable P : Type{â„“â‚š}
private variable ğ” : Model(P)
private variable Î“ Î“â‚ Î“â‚‚ : Formulas(P){â„“}
private variable Ï† Ïˆ : Formula(P)

[âŠ§â‚Š]-antimonotone : (Î“â‚ âŠ† Î“â‚‚) â†’ ((_âŠ§â‚Š Î“â‚) âŠ‡ (_âŠ§â‚Š Î“â‚‚))
[âŠ§â‚Š]-antimonotone Î“â‚Î“â‚‚ ğ”Î“â‚‚ Î“â‚Î³ = ğ”Î“â‚‚ (Î“â‚Î“â‚‚ Î“â‚Î³)

[âŠ§â‚Š]-strengthen : (ğ” âŠ§â‚Š Î“â‚) â†’ (ğ” âŠ§â‚Š Î“â‚‚) â†’ (ğ” âŠ§â‚Š (Î“â‚ âˆª Î“â‚‚))
[âŠ§â‚Š]-strengthen ğ”Î“â‚ ğ”Î“â‚‚ Î“â‚Î“â‚‚Î³ = Logic.[âˆ¨]-elim ğ”Î“â‚ ğ”Î“â‚‚ Î“â‚Î“â‚‚Î³

[âŠ§â‚Š]-of-[âˆª]â‚— : (ğ” âŠ§â‚Š (Î“â‚ âˆª Î“â‚‚)) â†’ (ğ” âŠ§â‚Š Î“â‚)
[âŠ§â‚Š]-of-[âˆª]â‚— ğ”Î“â‚Î“â‚‚ ğ”Î“â‚ = ğ”Î“â‚Î“â‚‚ (Left ğ”Î“â‚)

[âŠ§â‚Š]-of-[âˆª]áµ£ : (ğ” âŠ§â‚Š (Î“â‚ âˆª Î“â‚‚)) â†’ (ğ” âŠ§â‚Š Î“â‚‚)
[âŠ§â‚Š]-of-[âˆª]áµ£ ğ”Î“â‚Î“â‚‚ ğ”Î“â‚‚ = ğ”Î“â‚Î“â‚‚ (Right ğ”Î“â‚‚)

[âŠ§]-to-[âŠ§â‚Š] : (ğ” âŠ§ Ï†) Logic.â†” (ğ” âŠ§â‚Š singleton(Ï†))
[âŠ§]-to-[âŠ§â‚Š] = Logic.[â†”]-intro (_$ [â‰¡]-intro) (\ğ”Ï† Ï†Î³ â†’ substituteâ‚‚-â‚‚áµ£(_âŠ§_)(_) Ï†Î³ ğ”Ï†)

[âŠ§]-contradiction : (ğ” âŠ§ Ï†) â†’ (ğ” âŠ§ (Â¬ Ï†)) â†’ (ğ” âŠ§ âŠ¥)
[âŠ§]-contradiction = apply

[âŠ¨]-monotone : (Î“â‚ âŠ† Î“â‚‚) â†’ ((Î“â‚ âŠ¨_) âŠ† (Î“â‚‚ âŠ¨_))
[âŠ¨]-monotone Î“â‚Î“â‚‚ Î“â‚Ï† ğ”Î“â‚‚ = Î“â‚Ï† (Î“â‚Î³ â†¦ ğ”Î“â‚‚ (Î“â‚Î“â‚‚ Î“â‚Î³))

[âŠ¨]-functionâ‚— : (Î“â‚ â‰¡â‚› Î“â‚‚) â†’ ((Î“â‚ âŠ¨_) â‰¡â‚› (Î“â‚‚ âŠ¨_))
[âŠ¨]-functionâ‚— {Î“â‚ = Î“â‚}{Î“â‚‚ = Î“â‚‚} Î“â‚Î“â‚‚ {Ï†} = Logic.[â†”]-intro ([âŠ¨]-monotone{Î“â‚ = Î“â‚‚}{Î“â‚‚ = Î“â‚}(\{x} â†’ [â‰¡]-to-[âŠ‡] (Î“â‚Î“â‚‚{x}) {x}){Ï†}) ([âŠ¨]-monotone{Î“â‚ = Î“â‚}{Î“â‚‚ = Î“â‚‚}(\{x} â†’ [â‰¡]-to-[âŠ†] (Î“â‚Î“â‚‚{x}) {x}){Ï†})

[âŠ¨]-weaken : (Î“â‚ âŠ¨ Ï†) â†’ ((Î“â‚ âˆª Î“â‚‚) âŠ¨ Ï†)
[âŠ¨]-weaken Î“â‚Ï† ğ”Î“â‚Î“â‚‚ = Î“â‚Ï† (Î“â‚Î³ â†¦ ğ”Î“â‚Î“â‚‚ (Left Î“â‚Î³))

[âŠ¨]-validity : (âˆ€{Î“ : Formulas(P){â„“}} â†’ (Î“ âŠ¨ Ï†)) Logic.â†” Valid(Ï†)
[âŠ¨]-validity = Logic.[â†”]-intro (Î» r â†’ const r) (Î» l â†’ l{âˆ…} empty)

[âŠ¨]-contradiction : (Î“ âŠ¨ Ï†) â†’ (Î“ âŠ¨ (Â¬ Ï†)) â†’ (Î“ âŠ¨ âŠ¥)
[âŠ¨]-contradiction Î“Ï† Î“Â¬Ï† ğ”Î“ = Î“Â¬Ï† ğ”Î“ (Î“Ï† ğ”Î“)

[âŠ¨]-unsatisfiability : (Î“ âŠ¨ âŠ¥) Logic.â†” Unsatisfiable(Î“)
[âŠ¨]-unsatisfiability {Î“ = Î“} = Logic.[â†”]-intro l r where
  l : (Î“ âŠ¨ âŠ¥) â† Unsatisfiable(Î“)
  l unsatÎ“ {ğ”} ğ”Î“ = unsatÎ“ (Logic.[âˆƒ]-intro ğ” â¦ƒ ğ”Î“ â¦„)

  r : (Î“ âŠ¨ âŠ¥) â†’ Unsatisfiable(Î“)
  r Î“âŠ¥ (Logic.[âˆƒ]-intro ğ” â¦ƒ ğ”Î“ â¦„) = Î“âŠ¥ ğ”Î“

[âŠ¨][Â¬]-intro : ((Î“ âˆª singleton(Ï†)) âŠ¨ âŠ¥) Logic.â†” (Î“ âŠ¨ (Â¬ Ï†))
[âŠ¨][Â¬]-intro {Î“ = Î“}{Ï† = Ï†} = Logic.[â†”]-intro l r where
  l : ((Î“ âˆª singleton(Ï†)) âŠ¨ âŠ¥) â† (Î“ âŠ¨ (Â¬ Ï†))
  l Î“Â¬Ï† ğ”Î“Ï† = Î“Â¬Ï† (ğ”Î“Ï† âˆ˜ Left) (ğ”Î“Ï† (Right [â‰¡]-intro))

  r : ((Î“ âˆª singleton(Ï†)) âŠ¨ âŠ¥) â†’ (Î“ âŠ¨ (Â¬ Ï†))
  r Î“Ï†âŠ¥ ğ”Î“ ğ”Ï† = Î“Ï†âŠ¥ ([âŠ§â‚Š]-strengthen {Î“â‚ = Î“}{Î“â‚‚ = singleton(Ï†)} ğ”Î“ (Logic.[â†”]-to-[â†’] [âŠ§]-to-[âŠ§â‚Š] ğ”Ï†))

[âˆ…]-satisfiable : Satisfiable{P = P}{â„“ = â„“}(âˆ…)
[âˆ…]-satisfiable = Logic.[âˆƒ]-intro (const(ğ‘‡)) â¦ƒ \{_}() â¦„

module _ â¦ƒ classical : âˆ€{â„“} â†’ Logic.âˆ€â‚—(Classical{â„“}) â¦„ where
  [âŠ§]-of-[Â¬Â¬] : (ğ” âŠ§ Â¬(Â¬ Ï†)) â†’ (ğ” âŠ§ Ï†)
  [âŠ§]-of-[Â¬Â¬] {ğ” = ğ”}{Ï† = Ï†} = Logic.[Â¬Â¬]-elim

  [âŠ¨]-entailment-unsatisfiability : (Î“ âŠ¨ Ï†) Logic.â†” Unsatisfiable(Î“ âˆª singleton(Â¬ Ï†))
  [âŠ¨]-entailment-unsatisfiability {Î“ = Î“}{Ï† = Ï†} = Logic.[â†”]-intro l r where
    l : (Î“ âŠ¨ Ï†) â† Unsatisfiable(Î“ âˆª singleton(Â¬ Ï†))
    l r {ğ”} ğ”Î“ = [âŠ§]-of-[Â¬Â¬] {ğ” = ğ”}{Ï† = Ï†} (ğ”Â¬Ï† â†¦ r (Logic.[âˆƒ]-intro ğ” â¦ƒ Logic.[âˆ¨]-elim ğ”Î“ (\{[â‰¡]-intro â†’ ğ”Â¬Ï†}) â¦„))

    r : (Î“ âŠ¨ Ï†) â†’ Unsatisfiable(Î“ âˆª singleton(Â¬ Ï†))
    r l (Logic.[âˆƒ]-intro ğ” â¦ƒ sat â¦„) = [âŠ§]-contradiction {Ï† = Ï†} ğ”Ï† ğ”Â¬Ï† where
      ğ”Ï†  = l([âŠ§â‚Š]-of-[âˆª]â‚— {Î“â‚ = Î“} sat)
      ğ”Â¬Ï† = Logic.[â†”]-to-[â†] [âŠ§]-to-[âŠ§â‚Š] ([âŠ§â‚Š]-of-[âˆª]áµ£ {Î“â‚ = Î“} sat)

  [âŠ¨][âŸ¶]-intro : ((Î“ âˆª singleton(Ï†)) âŠ¨ Ïˆ) Logic.â†” (Î“ âŠ¨ (Ï† âŸ¶ Ïˆ))
  [âŠ¨][âŸ¶]-intro {Î“ = Î“}{Ï† = Ï†}{Ïˆ = Ïˆ} = Logic.[â†”]-intro l r where
    l : (Î“ âŠ¨ (Ï† âŸ¶ Ïˆ)) â†’ ((Î“ âˆª singleton(Ï†)) âŠ¨ Ïˆ)
    l Ï†Ïˆ {ğ” = ğ”} ğ”Î“Ï† = Logic.[âˆ¨]-elim (Â¬Ï† â†¦ Logic.[âŠ¥]-elim(Â¬Ï† ğ”Ï†)) id (Ï†Ïˆ ğ”Î“) where
      ğ”Î“ : ğ” âŠ§â‚Š Î“
      ğ”Î“ {Î³} Î“Î³ = ğ”Î“Ï† {Î³} (Logic.[âˆ¨]-introâ‚— Î“Î³)

      ğ”Ï† : ğ” âŠ§ Ï†
      ğ”Ï† = ğ”Î“Ï† {Ï†} (Logic.[âˆ¨]-introáµ£ [â‰¡]-intro)

    r : ((Î“ âˆª singleton(Ï†)) âŠ¨ Ïˆ) â†’ (Î“ âŠ¨ (Ï† âŸ¶ Ïˆ))
    r Î“Ï†Ïˆ {ğ” = ğ”} ğ”Î“ with Logic.excluded-middle(ğ” âŠ§ Ï†)
    ... | Logic.[âˆ¨]-introâ‚— ğ”Ï†  = Logic.[âˆ¨]-introáµ£ (Î“Ï†Ïˆ(Logic.[âˆ¨]-elim ğ”Î“ \{[â‰¡]-intro â†’ ğ”Ï†}))
    ... | Logic.[âˆ¨]-introáµ£ Â¬ğ”Ï† = Logic.[âˆ¨]-introâ‚— Â¬ğ”Ï†
