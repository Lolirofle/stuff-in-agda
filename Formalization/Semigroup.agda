module Formalization.Semigroup where

open import Data.ListNonEmpty as List using (Listâ‚Š ; _++_ ; âˆ… ; _âŠ°_)
import      Lvl
open import Numeral.Finite using (ğ•Ÿ)
open import Numeral.Natural using (â„•)
open import Type

private variable â„“ â„“â‚‘ : Lvl.Level
private variable T : Type{â„“}
private variable n nâ‚ nâ‚‚ : â„•

-- A term in the language of a semigroup.
-- It consists of a finite number of variables and a binary operator on its elements.
data Term (n : â„•) : Type{Lvl.ğŸ} where
  var : ğ•Ÿ(n) â†’ Term(n)
  _â–«_ : Term(n) â†’ Term(n) â†’ Term(n)

-- A fully normalised term in the language of a monoid is always able to be represented as a list of variables.
-- See `normalize` for how a term is represented as a list.
NormalForm : â„• â†’ Type
NormalForm(n) = Listâ‚Š(ğ•Ÿ(n))

-- Normalizes a term to its normal form.
normalize : Term(n) â†’ NormalForm(n)
normalize (var v) = List.singleton v
normalize (x â–« y) = normalize x ++ normalize y

open import Structure.Operator
open import Structure.Operator.Properties
open import Structure.Setoid

module Semantics â¦ƒ equiv : Equiv{â„“â‚‘}(T) â¦„ (_âˆ™_ : T â†’ T â†’ T) â¦ƒ oper : BinaryOperator(_âˆ™_) â¦„ â¦ƒ assoc : Associativity(_âˆ™_) â¦„ where
  -- The environment for evaluating an expression is just a mapping from its variables to the values.
  Env : â„• â†’ Type
  Env(n) = (ğ•Ÿ(n) â†’ T)

  -- The semantics of the terms.
  eval : Env(n) â†’ Term(n) â†’ T
  eval env (var n) = env n
  eval env (x â–« y) = eval env x âˆ™ eval env y

  -- The semantics of the normal form.
  evalNormal : Env(n) â†’ NormalForm(n) â†’ T
  evalNormal env (List.singleton v) = env v
  evalNormal env (v âŠ° l@(_ âŠ° _))    = env v âˆ™ evalNormal env l

  module Proofs where
    open import Structure.Function.Multi
    open import Structure.Relator.Properties
    open import Syntax.Transitivity

    private variable env : Env(n)
    private variable e : Term(n)

    evalNormal-step : âˆ€{x}{l} â†’ (evalNormal env (x âŠ° l) â‰¡ env x âˆ™ evalNormal env l)
    evalNormal-step {l = List.singleton y} = reflexivity(_â‰¡_)
    evalNormal-step {l = y âŠ° l@(_ âŠ° _)}    = reflexivity(_â‰¡_)

    instance
      eval-preserves : Preservingâ‚‚(eval env)(_â–«_)(_âˆ™_)
      eval-preserves = intro(reflexivity(_â‰¡_))

    instance
      evalNormal-preserves : Preservingâ‚‚(evalNormal env)(_++_)(_âˆ™_)
      evalNormal-preserves {env = env} = intro(\{x y} â†’ proof{x}{y}) where
        proof : Names.Preservingâ‚‚(evalNormal env)(_++_)(_âˆ™_)
        proof {List.singleton x} {ys} = evalNormal-step{x = x}{ys}
        proof {x âŠ° xs@(_ âŠ° _)}   {ys} =
          evalNormal env ((x âŠ° xs) ++ ys)                 ğŸ–[ _â‰¡_ ]-[]
          evalNormal env (x âŠ° (xs ++ ys))                 ğŸ–[ _â‰¡_ ]-[ evalNormal-step{x = x}{l = xs ++ ys} ]
          env x âˆ™ evalNormal env (xs ++ ys)               ğŸ–[ _â‰¡_ ]-[ congruenceâ‚‚-â‚‚(_âˆ™_)(_) (proof {xs}{ys}) ]
          env x âˆ™ (evalNormal env xs âˆ™ evalNormal env ys) ğŸ–[ _â‰¡_ ]-[ associativity(_âˆ™_) ]-sym
          (env x âˆ™ evalNormal env xs) âˆ™ evalNormal env ys ğŸ–[ _â‰¡_ ]-[]
          evalNormal env (x âŠ° xs) âˆ™ evalNormal env ys     ğŸ–-end

    normalize-eval-correctness : (evalNormal env (normalize e) â‰¡ eval env e)
    normalize-eval-correctness {env = env} {var v} = reflexivity(_â‰¡_)
    normalize-eval-correctness {env = env} {x â–« y} =
      evalNormal env (normalize (x â–« y))                          ğŸ–[ _â‰¡_ ]-[]
      evalNormal env (normalize x ++ normalize y)                 ğŸ–[ _â‰¡_ ]-[ preservingâ‚‚(evalNormal env)(_++_)(_âˆ™_) {normalize x}{normalize y} ]
      evalNormal env (normalize x) âˆ™ evalNormal env (normalize y) ğŸ–[ _â‰¡_ ]-[ congruenceâ‚‚(_âˆ™_) (normalize-eval-correctness{env = env}{x}) (normalize-eval-correctness{env = env}{y}) ]
      eval env x âˆ™ eval env y                                     ğŸ–[ _â‰¡_ ]-[]
      eval env (x â–« y)                                            ğŸ–-end
